buildscript {
    repositories {
        maven { url = 'https://files.minecraftforge.net/maven' }
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '3.+', changing: true
    }
}

apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'maven-publish'

import net.minecraftforge.gradle.common.task.SignJar

if (file('secrets.properties').exists()) {
    println "Loading parameters from secrets.properties"
    ext.secrets = new Properties()
    file('secrets.properties').withReader { ext.secrets.load(it) }
}

version = "${mc_version}-${mayor}.${noapi}.${minor}.${patch}${extra}"
group = "arekkuusu"
archivesBaseName = "ImprobablePlotMachine"

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8' // Need this here so eclipse task generates correctly.

minecraft {
    mappings channel: "${mc_mappings_channel}", version: "${mc_mappings_version}"
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    runs {
        client {
            workingDirectory project.file('run')
            properties 'fml.earlyprogresswindow': 'false'
            properties 'forge.logging.console.level': 'debug'

            mods {
                improbableplotmachine {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            properties 'forge.logging.console.level': 'debug'

            mods {
                improbableplotmachine {
                    source sourceSets.main
                }
            }
        }

        data {
            properties 'fml.earlyprogresswindow': 'false'
            workingDirectory project.file('run')
            forceExit false

            mods {
                improbableplotmachine {
                    source sourceSets.main
                }
            }
            args '--mod', 'improbableplotmachine', '--all',
                    '--existing', '"' + file('src/main/resources/') + '"',
                    '--existing', '"' + file('src/generated/resources/') + '"',
                    '--output', '"' + file('src/generated/resources/') + '"'
        }
    }
}

task deobfJar(type: Jar) {
    from(sourceSets.main.output)
    archiveName = "${archivesBaseName}-${version}-deobf.${extension}"
}

artifacts {
    archives deobfJar
}

repositories {

}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
}

jar {
    exclude "**/*.bat"
    exclude "**/*.psd"
    exclude "**/*.txt"
    exclude "**/*.d"
    manifest {
        attributes([
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

task signJar(type: SignJar, dependsOn: jar) {

    keyStore = project.ext.secrets.keyStore // This needs to be a path to the keystore file
    alias = project.ext.secrets.keyStoreAlias
    storePass = project.ext.secrets.keyStorePass
    keyPass = project.ext.secrets.keyStoreKeyPass
    inputFile = jar.archivePath
    outputFile = jar.archivePath
}

build.dependsOn signJar

jar.finalizedBy('reobfJar')

def reobfFile = file("$buildDir/reobfJar/output.jar")
def reobfArtifact = artifacts.add('default', reobfFile) {
    //noinspection GroovyAssignabilityCheck
    type 'jar'
    builtBy 'reobfJar'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact reobfArtifact
        }
    }
    repositories {
        maven {
            url "file:///${project.projectDir}/mcmodsrepo"
        }
    }
}

sourceSets.main.resources {
    srcDir 'src/generated/resources'
}